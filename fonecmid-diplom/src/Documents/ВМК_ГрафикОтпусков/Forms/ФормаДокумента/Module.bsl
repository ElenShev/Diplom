
&НаКлиенте
Процедура АнализГрафика(Команда)
    
    СсылкаДокумента = Объект.Ссылка;
 
    АдресДанных = ПолучитьДанныеДляДиаграммы();
    
    ПараметрыФормы = Новый Структура;
    ПараметрыФормы.Вставить("СсылкаНаДокумент", СсылкаДокумента);
    ПараметрыФормы.Вставить("АдресДанныхДиаграммы", АдресДанных);
    
    ОткрытьФорму("Документ.ВМК_ГрафикОтпусков.Форма.АнализГрафика", ПараметрыФормы);
    
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеДляДиаграммы()
    
    Запрос = Новый Запрос;
    Запрос.Текст =
        "ВЫБРАТЬ
        |    ВМК_ГрафикОтпусковГрафикОтпусков.Сотрудник,
        |    ВМК_ГрафикОтпусковГрафикОтпусков.ДатаНачала,
        |    ВМК_ГрафикОтпусковГрафикОтпусков.ДатаОкончания,
        |    ВМК_ГрафикОтпусковГрафикОтпусков.Сотрудник.Специализация
        |ИЗ
        |    Документ.ВМК_ГрафикОтпусков.ГрафикОтпусков КАК ВМК_ГрафикОтпусковГрафикОтпусков
        |ГДЕ
        |    ВМК_ГрафикОтпусковГрафикОтпусков.Ссылка = &Ссылка";
    
    Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
    
    РезультатЗапроса = Запрос.Выполнить();
    ДанныеДиаграммы = РезультатЗапроса.Выгрузить();

    АдресДанных = ПоместитьВоВременноеХранилище(ДанныеДиаграммы, УникальныйИдентификатор);
    
    Возврат АдресДанных;
    
КонецФункции

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
    УстановитьПодсветку();
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    УстановитьПодсветку();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
    УстановитьПодсветку();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПодсветку()
    УстановитьПодсветкуНаСервере();
    Элементы.ГрафикОтпусков.Обновить();
КонецПроцедуры

&НаСервере
Процедура УстановитьПодсветкуНаСервере()

    Запрос = Новый Запрос;
    Запрос.Текст = "ВЫБРАТЬ
    |    ВМК_ГрафикОтпусковГрафикОтпусков.Сотрудник КАК Сотрудник,
    |    СУММА(РАЗНОСТЬДАТ(ВМК_ГрафикОтпусковГрафикОтпусков.ДатаНачала, ВМК_ГрафикОтпусковГрафикОтпусков.ДатаОкончания, ДЕНЬ) + 1) КАК ДнейОтпуска
    |ИЗ
    |    Документ.ВМК_ГрафикОтпусков.ГрафикОтпусков КАК ВМК_ГрафикОтпусковГрафикОтпусков
    |        ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВМК_ГрафикОтпусков КАК ВМК_ГрафикОтпусков
    |        ПО (ВМК_ГрафикОтпусковГрафикОтпусков.Ссылка = ВМК_ГрафикОтпусков.Ссылка)
    |ГДЕ
    |    ВМК_ГрафикОтпусков.Ссылка = &Ссылка
    |
    |СГРУППИРОВАТЬ ПО
    |    ВМК_ГрафикОтпусковГрафикОтпусков.Сотрудник";

    Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
    
    Результат = Запрос.Выполнить().Выбрать();

    Для Каждого Стр Из Объект.ГрафикОтпусков Цикл
        Стр.Выделять = Ложь;
    КонецЦикла;

    Пока Результат.Следующий() Цикл

        Если Результат.ДнейОтпуска > 28 Тогда

            Для Каждого Стр Из Объект.ГрафикОтпусков Цикл
                Если Стр.Сотрудник = Результат.Сотрудник Тогда
                    Стр.Выделять = Истина;
                КонецЕсли;
            КонецЦикла;

        КонецЕсли;

    КонецЦикла;

КонецПроцедуры